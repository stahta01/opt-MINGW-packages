# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

OPT_MINGW_PACKAGE_PREFIX="opt-mingw-$MSYSTEM_CARCH"
OPT_MINGW_PREFIX="/opt$MINGW_PREFIX"

_realname=crt
_sourcedir=mingw-w64-git
pkgbase=mingw-w64-${_realname}2
pkgname=("${OPT_MINGW_PACKAGE_PREFIX}-${_realname}2-git" "${OPT_MINGW_PACKAGE_PREFIX}-headers2-git" "${OPT_MINGW_PACKAGE_PREFIX}-pthread-stub-headers2-git")
replaces=()
pkgver=2.0.0.2777.24b1f9cdc
pkgrel=2
_commit='24b1f9cdcf1d425700312373b14e2a6e58c6d0aa'
pkgdesc='MinGW-w64 CRT for Windows'
arch=('any')
url='https://mingw-w64.sourceforge.io/'
license=('custom')
groups=("${OPT_MINGW_PACKAGE_PREFIX}-toolchain")
makedepends=('git'
             "${MINGW_PACKAGE_PREFIX}-gcc-base"
             "${MINGW_PACKAGE_PREFIX}-binutils")
options=('!strip' 'staticlibs' '!buildflags' '!emptydirs')
source=("${_sourcedir}"::"git+https://git.code.sf.net/p/mingw-w64/mingw-w64#commit=$_commit"
        001-Use-do_pseudo_reloc-void-start-void-end-void-base-re.patch)
sha256sums=('SKIP'
            '6b5106c814286c60e29030a5e9080adbe58dabba992123a57ae55df43399ca79')

# Declare global variables; begin with underscore to avoid name conflicts
_git_base_commit=

pkgver() {
  cd "${srcdir}/${_sourcedir}"
  local _major=$(head -n 16 mingw-w64-headers/crt/_mingw_mac.h | grep '__MINGW64_VERSION_MAJOR' | head -n 1 | sed -e 's/MINGW64//' | sed -e 's/.* //' | tr '_[A-Z]\n' ' ' | sed 's/.$/\n/')
  local _minor=$(head -n 16 mingw-w64-headers/crt/_mingw_mac.h | grep '__MINGW64_VERSION_MINOR' | head -n 1 | sed -e 's/MINGW64//' | sed -e 's/.* //' | tr '_[A-Z]\n' ' ' | sed 's/.$/\n/')
  local _rev=0
  printf "%s.%s.%s.%s.%s" ${_major} ${_minor} ${_rev} "$(git rev-list --count ${_git_base_commit})" "$(git rev-parse --short ${_git_base_commit})"
}

prepare() {
  cd "${srcdir}/${_sourcedir}"

  _git_base_commit=$(git rev-parse HEAD)

  git am --committer-date-is-author-date "${srcdir}/001-Use-do_pseudo_reloc-void-start-void-end-void-base-re.patch"
}

build() {
  msg "Configuring ${MINGW_CHOST} headers"
  [[ -d ${srcdir}/headers-${MINGW_CHOST} ]] && rm -rf ${srcdir}/headers-${MINGW_CHOST}
  mkdir -p ${srcdir}/headers-${MINGW_CHOST} && cd ${srcdir}/headers-${MINGW_CHOST}

  export PATH=${OPT_MINGW_PREFIX}/bin:$PATH

  ${srcdir}/${_sourcedir}/mingw-w64-headers/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${OPT_MINGW_PREFIX} \
    --enable-sdk=all \
    --enable-secure-api

  msg "Installing local copy of ${MINGW_CHOST} headers"
  cd ${srcdir}/headers-${MINGW_CHOST}
  rm -fr ${srcdir}/${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include
  make DESTDIR=${srcdir} install

  msg "Building ${MINGW_CHOST} CRT"
  case "$CARCH" in
    i686)
      _crt_configure_args="--disable-lib64 --enable-lib32"
    ;;
    x86_64)
      _crt_configure_args="--disable-lib32 --enable-lib64"
    ;;
  esac

  [[ -d ${srcdir}/crt-${MINGW_CHOST} ]] && rm -rf ${srcdir}/crt-${MINGW_CHOST}
  mkdir -p ${srcdir}/crt-${MINGW_CHOST} && cd ${srcdir}/crt-${MINGW_CHOST}

#  export LDFLAGS+=" --allow-multiple-definition"
# -nostdinc 

  CFLAGS+=" -I${srcdir}/${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include" \
  ${srcdir}/${_sourcedir}/mingw-w64-crt/configure \
    --prefix=${OPT_MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-sysroot=${srcdir}/${OPT_MINGW_PREFIX} \
    --enable-wildcard \
    ${_crt_configure_args}
  make V=1
}

package_crt() {
  pkgdesc='MinGW-w64 CRT for Windows'
  provides=("${OPT_MINGW_PACKAGE_PREFIX}-${_realname}")
  conflicts=("${OPT_MINGW_PACKAGE_PREFIX}-${_realname}")

  msg "Installing ${MINGW_CHOST} crt"
  cd ${srcdir}/crt-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install-strip
}

package_pthread_stub_headers() {
    pkgdesc="MinGW-w64 pthread stub headers for Windows"
    provides=("${OPT_MINGW_PACKAGE_PREFIX}-pthread-stub-headers")
    conflicts=("${OPT_MINGW_PACKAGE_PREFIX}-pthread-stub-headers" "${OPT_MINGW_PACKAGE_PREFIX}-winpthreads")
    msg "Copying ${MINGW_CHOST} pthread stub headers"

    mkdir -p ${pkgdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include

#    cp ${srcdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_signal.h ${pkgdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include/
    cp ${srcdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_time.h   ${pkgdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include/
    cp ${srcdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_unistd.h ${pkgdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include/
}

package_headers() {
  pkgdesc="MinGW-w64 headers for Windows"
  provides=("${OPT_MINGW_PACKAGE_PREFIX}-headers")
  conflicts=("${OPT_MINGW_PACKAGE_PREFIX}-headers")

  msg "Copying ${MINGW_CHOST} headers"
  mkdir -p ${pkgdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}
  cp --recursive ${srcdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include ${pkgdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/

#  rm ${pkgdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_signal.h
  rm ${pkgdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_time.h
  rm ${pkgdir}${OPT_MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_unistd.h

  msg "Installing MinGW-w64 licenses"
  install -Dm644 ${srcdir}/${_sourcedir}/mingw-w64-headers/ddk/readme.txt ${pkgdir}${OPT_MINGW_PREFIX}/share/licenses/${_realname}/ddk-readme.txt
  install -Dm644 ${srcdir}/${_sourcedir}/mingw-w64-headers/direct-x/COPYING.LIB ${pkgdir}${OPT_MINGW_PREFIX}/share/licenses/${_realname}/direct-x-COPYING.LIB
  install -Dm644 ${srcdir}/${_sourcedir}/mingw-w64-headers/direct-x/readme.txt ${pkgdir}${OPT_MINGW_PREFIX}/share/licenses/${_realname}/direct-x-readme.txt
}

package_opt-mingw-i686-crt2-git() {
  package_crt
}
package_opt-mingw-i686-headers2-git() {
  package_headers
}
package_opt-mingw-i686-pthread-stub-headers2-git() {
  package_pthread_stub_headers
}
package_opt-mingw-x86_64-crt2-git() {
  package_crt
}
package_opt-mingw-x86_64-headers2-git() {
  package_headers
}
package_opt-mingw-x86_64-pthread-stub-headers2-git() {
  package_pthread_stub_headers
}
